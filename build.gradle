plugins {
	id 'org.springframework.boot' version '2.1.6.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id 'java'
}



group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
	mavenCentral()
}

dependencies {
	implementation "org.springframework.boot:spring-boot-starter-web"
	testImplementation "org.springframework.boot:spring-boot-starter-test"
	testImplementation "com.intuit.karate:karate-apache:0.9.5"
	testImplementation "com.intuit.karate:karate-junit4:0.9.5"
	testImplementation "net.masterthought:cucumber-reporting:3.8.0"
}

sourceSets {
	test {
		java {
			srcDir file('src/test/java')
			exclude '**/*UiRunner*.java'
		}
		resources {
			srcDir file('src/test/java')
			exclude '**/*.java'
		}
	}
}

test {
	include '**/*KarateTests*'
	include '**/*DemoTestParallel*'
	systemProperty "karate.options", System.properties.getProperty("karate.options")
	systemProperty "karate.env", System.properties.getProperty("karate.env")
	outputs.upToDateWhen { false }
}

bootRun {
	main = 'com.example.demo.DemoApplication'
}

task karateDebug(type: JavaExec) {
	classpath = sourceSets.test.runtimeClasspath
	main = 'com.intuit.karate.cli.Main'
	args  'src/test/java/karate'
}

wrapper {
	gradleVersion = 4.9
}
